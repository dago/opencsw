#!/opt/csw/bin/perl -lw
#
# $Id$
# $URL$
#
# cswproto - Fix up package prototypes according to CSW standards.
#

use strict;
use Getopt::Long qw/:config no_ignore_case/;
use vars qw/
    $opt_h $opt_s %srcvars %cmpvars @XPATS @XFORMS %Common
    $StdOwn $StdGrp $StdDirPerm
/;
# atime=8,mtime=9,ctime=10
use constant TIME_FIELD => 10;

# Prototype defaults
$StdOwn     = 'root';
$StdGrp     = 'bin';
$StdDirPerm = '0755';

# Path transforms
@XFORMS = (
    [ qr{^/opt/csw/man$},  q{/opt/csw/share/man}  ],
    [ qr{^/opt/csw/doc$},  q{/opt/csw/share/doc}  ],
    [ qr{^/opt/csw/info$}, q{/opt/csw/share/info} ],
);

# Standard exclusions
@XPATS = (
    qr{^/$},
    qr{^/opt$},
    qr{^/opt/csw$},
    qr{perllocal\.pod},
);

# Print usage information and exit
sub usage {
    print ($@, "\n") if $@;

    die <<"_USAGE_";
Usage: $0 [-h] [-s <timestamp>] [-v var=value] [-V var=value]
       path1[=path1] ... pathN[=pathN]

    -s      Timestamp source.
            The path to a file to be used as the base timestamp for prototype
            operations.  If this is specified, all source file creation times
            are compared to the creation time of this file.  Files created
            *before* this time will be excluded from the prototype.

    -v      Source replacement variable.
            This is a name=value pair to be used for inserting replacement
            variables into the prototype.  The -v flag will replace parts of
            the source path only with variable names which can be supplied to
            pkgmk at package creation time.  This option can be specified
            multiple times.

    -V      Replacement variable.
            This is a name=value pair to be used for inserting replacement
            variables into the prototype.  The -v flag will replace parts of
            each prototype path with variable names which can be supplied to
            pkgmk at package creation time.  This option can be specified
            multiple times.

    -h      Display brief usage.

    pathN   The remainder of arguments to this command will be specified
            directly to pkgproto.  These arguments specify which paths are to
            be included in the prototype.  This can be specified as
            pathX=pathY to use a different prefix for files in the prototype
            than were present on the build system.

_USAGE_
}

# Do variable replacements in a text string
sub replace {
    my ($rdata, $text) = @_;
    return unless $rdata and (ref($rdata) eq 'HASH') and $text;
    $$text =~ s/$rdata->{$_}/\$$_/g foreach (keys %$rdata);
}

#
# MAIN EXECUTION
#

# Process command line arguments
my $test;
GetOptions(
    'v=s%'    => \%srcvars,
    'V=s%'    => \%cmpvars,
    'stamp=s' => \$opt_s,
    'help'    => \$opt_h,
) or usage;

usage "error: timestamp not found\n" if $opt_s and ! -r $opt_s;
usage "error: one or more pkgproto patterns required\n" unless @ARGV;

my $s_time = $opt_s ? (stat($opt_s))[TIME_FIELD] : 0;

# Load common path contents
while (<DATA>) { chomp; $Common{$_} = 1 }

# Create the prototype
my $PPAT = join ' ' => @ARGV;
open PROTO, "pkgproto $PPAT |"
    or die "failed to open pipe to pkgproto: $!";

my @prototype;
SPECLINE:
while (<PROTO>) {
    do { print; next } if /^(?:i|!)/;
    chomp; my @F = split;

    # Only add this if it's newer than $s_time
    if (index($F[2], '=') != -1) {
        my ($f_dst, $f_src) = split /=/, $F[2], 2;
        $f_src = $f_dst unless $f_src;

        if (-r $f_src) {
            my $f_time = (stat($f_src))[TIME_FIELD];
            next unless $f_time >= $s_time;
        }

        if ($f_src ne $f_dst) {
            replace(\%srcvars, \$f_src);
            $F[2] = join '=', $f_dst, $f_src;
        }
    }

    # First process any excludes
    foreach my $pattern (@XPATS) { next SPECLINE if $F[2] =~ $pattern   }

    # Then do path transforms
    foreach my $xform (@XFORMS)  { $F[2] =~ s/$xform->[0]/$xform->[1]/g }

    # And finally, write out the line properly if the path is part
    # of the common set in DATA.  Also, fix up dir permissions/file
    # ownership.
    if ($Common{$F[2]}) {
        $F[3] = $F[4] = $F[5] = '?';
    }
    else {
        $F[3] = $StdDirPerm if $F[0] eq 'd';
        ($F[4], $F[5]) = ( $StdOwn, $StdGrp );
    }
    push @prototype, [ grep { defined $_ } @F ];
}
close PROTO;

# Do variable replacements, and 
foreach my $item (sort { $a->[2] cmp $b->[2] } @prototype) {
    my $line = join " " => @$item;
    replace(\%cmpvars, \$line);
    print $line;
}

# Common directories (CSWcommon plus a few others)
__DATA__
/opt/csw/share/perl
/opt/csw/share/perl/csw
/opt/csw/share/perl/site_perl
/opt/csw/lib/perl
/opt/csw/lib/perl/csw
/opt/csw/lib/perl/site_perl
/opt/csw
/opt/csw/bin
/opt/csw/bin/i486
/opt/csw/bin/pentium
/opt/csw/doc
/opt/csw/info
/opt/csw/lib
/opt/csw/lib/32
/opt/csw/lib/X11
/opt/csw/lib/X11/app-defaults
/opt/csw/lib/i386
/opt/csw/lib/locale
/opt/csw/lib/pentium
/opt/csw/man
/opt/csw/sbin
/opt/csw/share
/opt/csw/share/doc
/opt/csw/share/info
/opt/csw/share/locale
/opt/csw/share/locale/az
/opt/csw/share/locale/az/LC_MESSAGES
/opt/csw/share/locale/be
/opt/csw/share/locale/be/LC_MESSAGES
/opt/csw/share/locale/bg
/opt/csw/share/locale/bg/LC_MESSAGES
/opt/csw/share/locale/bg/LC_TIME
/opt/csw/share/locale/ca
/opt/csw/share/locale/ca/LC_MESSAGES
/opt/csw/share/locale/cs
/opt/csw/share/locale/cs/LC_MESSAGES
/opt/csw/share/locale/cs/LC_TIME
/opt/csw/share/locale/da
/opt/csw/share/locale/da/LC_MESSAGES
/opt/csw/share/locale/da/LC_TIME
/opt/csw/share/locale/de
/opt/csw/share/locale/de/LC_MESSAGES
/opt/csw/share/locale/de/LC_TIME
/opt/csw/share/locale/el
/opt/csw/share/locale/el/LC_MESSAGES
/opt/csw/share/locale/el/LC_TIME
/opt/csw/share/locale/en@boldquot
/opt/csw/share/locale/en@boldquot/LC_MESSAGES
/opt/csw/share/locale/en@quot
/opt/csw/share/locale/en@quot/LC_MESSAGES
/opt/csw/share/locale/es
/opt/csw/share/locale/es/LC_MESSAGES
/opt/csw/share/locale/es/LC_TIME
/opt/csw/share/locale/et
/opt/csw/share/locale/et/LC_MESSAGES
/opt/csw/share/locale/eu
/opt/csw/share/locale/eu/LC_MESSAGES
/opt/csw/share/locale/fi
/opt/csw/share/locale/fi/LC_MESSAGES
/opt/csw/share/locale/fr
/opt/csw/share/locale/fr/LC_MESSAGES
/opt/csw/share/locale/fr/LC_TIME
/opt/csw/share/locale/ga
/opt/csw/share/locale/ga/LC_MESSAGES
/opt/csw/share/locale/gl
/opt/csw/share/locale/gl/LC_MESSAGES
/opt/csw/share/locale/gl/LC_TIME
/opt/csw/share/locale/he
/opt/csw/share/locale/he/LC_MESSAGES
/opt/csw/share/locale/hr
/opt/csw/share/locale/hr/LC_MESSAGES
/opt/csw/share/locale/hu
/opt/csw/share/locale/hu/LC_MESSAGES
/opt/csw/share/locale/id
/opt/csw/share/locale/id/LC_MESSAGES
/opt/csw/share/locale/it
/opt/csw/share/locale/it/LC_MESSAGES
/opt/csw/share/locale/it/LC_TIME
/opt/csw/share/locale/ja
/opt/csw/share/locale/ja/LC_MESSAGES
/opt/csw/share/locale/ja/LC_TIME
/opt/csw/share/locale/ko
/opt/csw/share/locale/ko/LC_MESSAGES
/opt/csw/share/locale/ko/LC_TIME
/opt/csw/share/locale/locale.alias
/opt/csw/share/locale/lt
/opt/csw/share/locale/lt/LC_MESSAGES
/opt/csw/share/locale/nl
/opt/csw/share/locale/nl/LC_MESSAGES
/opt/csw/share/locale/nl/LC_TIME
/opt/csw/share/locale/nn
/opt/csw/share/locale/nn/LC_MESSAGES
/opt/csw/share/locale/no
/opt/csw/share/locale/no/LC_MESSAGES
/opt/csw/share/locale/no/LC_TIME
/opt/csw/share/locale/pl
/opt/csw/share/locale/pl/LC_MESSAGES
/opt/csw/share/locale/pl/LC_TIME
/opt/csw/share/locale/pt
/opt/csw/share/locale/pt/LC_MESSAGES
/opt/csw/share/locale/pt/LC_TIME
/opt/csw/share/locale/pt_BR
/opt/csw/share/locale/pt_BR/LC_MESSAGES
/opt/csw/share/locale/pt_BR/LC_TIME
/opt/csw/share/locale/ro
/opt/csw/share/locale/ro/LC_MESSAGES
/opt/csw/share/locale/ru
/opt/csw/share/locale/ru/LC_MESSAGES
/opt/csw/share/locale/ru/LC_TIME
/opt/csw/share/locale/sk
/opt/csw/share/locale/sk/LC_MESSAGES
/opt/csw/share/locale/sk/LC_TIME
/opt/csw/share/locale/sl
/opt/csw/share/locale/sl/LC_MESSAGES
/opt/csw/share/locale/sl/LC_TIME
/opt/csw/share/locale/sp
/opt/csw/share/locale/sp/LC_MESSAGES
/opt/csw/share/locale/sr
/opt/csw/share/locale/sr/LC_MESSAGES
/opt/csw/share/locale/sv
/opt/csw/share/locale/sv/LC_MESSAGES
/opt/csw/share/locale/sv/LC_TIME
/opt/csw/share/locale/tr
/opt/csw/share/locale/tr/LC_MESSAGES
/opt/csw/share/locale/uk
/opt/csw/share/locale/uk/LC_MESSAGES
/opt/csw/share/locale/vi
/opt/csw/share/locale/vi/LC_MESSAGES
/opt/csw/share/locale/wa
/opt/csw/share/locale/wa/LC_MESSAGES
/opt/csw/share/locale/zh
/opt/csw/share/locale/zh/LC_MESSAGES
/opt/csw/share/locale/zh/LC_TIME
/opt/csw/share/locale/zh_CN
/opt/csw/share/locale/zh_CN.GB2312
/opt/csw/share/locale/zh_CN.GB2312/LC_MESSAGES
/opt/csw/share/locale/zh_TW
/opt/csw/share/locale/zh_TW.Big5
/opt/csw/share/locale/zh_TW.Big5/LC_MESSAGES
/opt/csw/share/locale/zh_TW/LC_MESSAGES
/opt/csw/share/man
/opt/csw/var
